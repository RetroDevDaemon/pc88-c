10 'fm(opn) sound test
20 DEFINT A-Z:CH=0:OCT=3:FRQ=&H26A 'o4c
30 READ AL,FB
40 REG=&HB0+CH:DAT=FB*8+AL:GOSUB 460
50 OP=0:GOSUB 360:OP=2:GOSUB 360:OP=1:GOSUB 360:OP=3:GOSUB 360
60 '
70 REG=&HA4+CH:DAT=((OCT*2048+FRQ) AND &HFF00)/256:GOSUB 460
80 REG=&HA0+CH:DAT=((OCT*2048+FRQ) AND &HFF):GOSUB 460
90 REG=&H28:DAT=&HF0+CH:GOSUB 460
100 PRINT"PUSH SPACE TO KEYOFF"
110 IF INP(9)<>191 THEN 110
120 REG=&H28:DAT=CH:GOSUB 460
130 END
360 'write fm parameter to (op)
370 READ AR,DR,SR,RR,SL,TL,KS,ML,DT,SSG
380 REG=&H30+CH+OP*4:DAT=DT*16+ML:GOSUB 460
390 REG=&H40+CH+OP*4:DAT=TL:GOSUB 460
400 REG=&H50+CH+OP*4:DAT=KS*64+AR:GOSUB 460
410 REG=&H60+CH+OP*4:DAT=DR:GOSUB 460
420 REG=&H70+CH+OP*4:DAT=SR:GOSUB 460
430 REG=&H80+CH+OP*4:DAT=SL*16+RR:GOSUB 460
440 REG=&H90+CH+OP*4:DAT=SSG:GOSUB 460
450 RETURN
460 'write fm register
470 OUT &H44,REG
480 OUT &H45,DAT
490 RETURN
500 '    AL FB
510 DATA 2, 7
520 '    AR DR SR RR SL TL KS ML DT SSG-EG
530 DATA 31,12, 4,10, 1,32, 0,12, 0, 0
540 DATA 31, 2, 4, 6,15,57, 3,15, 1, 0
550 DATA 31,12, 4, 6, 0,30, 0, 1, 0, 0
560 DATA 31, 5, 7, 7, 2, 0, 2, 3, 5, 0
